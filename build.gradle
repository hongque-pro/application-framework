buildscript {
    ext {
        kotlin_version = '1.4.10'
        slf4j_version = '1.7.30'
        infra_caching_version='1.2.2'
        infra_commons_version='2.1.0'
        infra_oauth2_version = '1.1.1'
        infra_stream_version='1.0.0'

        uber_h3_version='3.7.0'
        netty_version='4.1.63.Final'
//        jts_version='1.13' 地理信息标准格式处理库，暂时不需要

        hibernate_validation_verion='6.2.0.Final'

        spring_boot_version = '2.4.5'
        spring_cloud_version='2020.0.2'
        spring_version = '5.3.6'
        spring_dep_mgn_version='1.0.11.RELEASE'

        kryo_version = '5.1.0'

        junit_verions = '5.7.0'

        //data base and mybatis
        mybatis_spring_boot_version='2.1.4'
        mysql_connector_version='8.0.22'
        mybatis_generator_version='1.4.0'
        mybatis_gen_plug_version='1.2.17'
        mybatis_version = '3.5.6'
        //for unit test
        h2_verion = '1.4.200'

        swagger_version='3.0.0'

        aliyun_sdk_version='4.5.20'
        aliyun_sdk_sts_version='3.0.2'
        aliyun_sdk_oss_version='3.12.0'
//        aliyun_sdk_afs_version='1.0.0'

        huawei_esdk_obs_version='3.20.6.2'

        typescript_gen_version='2.17.558'
        antisamy_version='1.5.10'
        bouncycastle_version='1.68'

        okhttp_version='4.9.1'
        fasterxml_version='2.12.3'
        apache_poi_version='5.0.0'
        kaptcha_version='2.3.2'
//        mockito_version = '1.10.19'
        mockito_core_version='2.24.0'

        maven_repo= System.getenv("MAVEN_PROXY")
        publishing_plugin_version = '1.1.0'
        git_properties_plugin_version = '2.3.1'

    }
    repositories {
        mavenLocal()
        if(maven_repo != null) {
            maven { url maven_repo }
        }
        mavenCentral()
        maven { url 'https://repo.spring.io/plugins-snapshot' }
    }
}

plugins {
    id "org.jetbrains.kotlin.plugin.spring" version "$kotlin_version" apply false
    id "org.springframework.boot" version "$spring_boot_version" apply false
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version" apply false
    id "io.spring.dependency-management" version "$spring_dep_mgn_version" apply false
    id "io.github.gradle-nexus.publish-plugin" version "$publishing_plugin_version" apply false
    id "com.gorylenko.gradle-git-properties" version "$git_properties_plugin_version" apply false
}
apply plugin: 'io.github.gradle-nexus.publish-plugin'

static Boolean  isNotNullOrBlank(Object s){
    return !(s == null || s.toString().trim().length() == 0)
}

static canBeSign(Project project) {
    return isNotNullOrBlank(project.findProperty("signing.password")) &&
            isNotNullOrBlank(project.findProperty('signing.secretKeyRingFile')) &&
            isNotNullOrBlank(project.findProperty('signing.keyId'))
}

static getPropertyOrCmdArgs(Project project, String propertyName, String cmdArgName) {
    def propertyValue = project.hasProperty(propertyName) ? project.property(propertyName).toString() : null
    def v = (System.properties.getProperty(cmdArgName) ?: propertyValue ) ?: System.getenv(propertyName)
    return isNotNullOrBlank(v) ? v : null
}

allprojects {
    group 'com.labijie.application'
    version '2.1.0'

    apply plugin: 'kotlin'
    apply plugin: 'signing'
    apply plugin: 'kotlin-spring'
    apply plugin: 'java-library'
    apply plugin: 'com.gorylenko.gradle-git-properties'
    if(!project.name.startsWith("dummy")) {
        apply plugin: 'maven-publish'
    }

    compileJava.options.encoding = "UTF-8"
    javadoc.options.encoding = "UTF-8"

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        failOnError = false
        options.encoding = 'utf-8'
    }

    tasks.withType(Javadoc).all { enabled = false }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    sourceCompatibility = 1.8

    repositories {
        mavenLocal()
        if(maven_repo != null) {
            maven { url maven_repo }
        }
        mavenCentral()
    }

    configurations.all {
        exclude group: "junit", module: "junit"
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }


    dependencies {
        implementation platform("org.jetbrains.kotlin:kotlin-bom:${kotlin_version}")
        implementation platform("org.springframework.boot:spring-boot-dependencies:${spring_boot_version}")
        implementation platform("org.springframework.cloud:spring-cloud-dependencies:${spring_cloud_version}")

        implementation "org.jetbrains.kotlin:kotlin-stdlib"
        implementation "org.jetbrains.kotlin:kotlin-reflect"



        testImplementation platform("org.junit:junit-bom:$junit_verions")
        testImplementation "org.jetbrains.kotlin:kotlin-test-junit5:$kotlin_version"
        // https://mvnrepository.com/artifact/org.mockito/mockito-all
        testImplementation "com.h2database:h2:$h2_verion"
        testImplementation "mysql:mysql-connector-java:$mysql_connector_version"
        testImplementation("org.springframework.boot:spring-boot-starter-test") {
            exclude group: "org.mockito", module: "mockito-all"
        }
        testImplementation "org.mybatis.spring.boot:mybatis-spring-boot-starter-test:$mybatis_spring_boot_version"

        testImplementation "org.junit.jupiter:junit-jupiter"
        testImplementation("org.jetbrains.kotlin:kotlin-test-junit5"){
            exclude group:"org.junit"
        }

        testImplementation "org.mockito:mockito-core:$mockito_core_version"

    }

    test {
        useJUnitPlatform()
    }
}

static def getMybatisConfigFilePath(String rootPath){
    return rootPath.endsWith(File.separator) ? rootPath.substring(0, rootPath.length() - 2) : rootPath +
            File.separator + "mybatis-conf" +
            File.separator + "config.xml"
}

static def getMybatisPropertiesFilePath(String rootPath){
    return rootPath.endsWith(File.separator) ? rootPath.substring(0, rootPath.length() - 2) : rootPath +
            File.separator + "mybatis-conf" +
            File.separator + "settings.properties"
}

subprojects {
    def u = getPropertyOrCmdArgs(project, "PUB_USER", "u")
    def p =getPropertyOrCmdArgs(project, "PUB_PWD", "p")
    def s = getPropertyOrCmdArgs(project, "PUB_URL", "s")

    if(!project.name.startsWith("dummy")) {
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    artifactId = "framework-" + project.name
                    from components.java
                    pom {
                        name = "framework-" + project.name
                        description = 'application framework package'
                        url = 'https://github.com/hongque-pro/application-framework'
//                    properties = [
//                            myProp: "value",
//                            "prop.with.dots": "anotherValue"
//                    ]
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        developers {
                            developer {
                                id = 'labijie infra team'
                                name = 'infra team'
                                email = 'tech@labijie.com'
                            }
                        }
                        scm {
                            url = 'https://github.com/hongque-pro/application-framework'
                            connection = 'scm:git@github.com:hongque-pro/application-framework.git'
                            developerConnection = 'git@github.com:hongque-pro/application-framework.git'
                        }
                    }
                }
            }

            signing {
                if (canBeSign(project)) {
                    sign publishing.publications.mavenJava
                } else {
                    println "Signing information missing/incomplete for ${project.name}"
                }
            }

            repositories {
                if (isNotNullOrBlank(u) && isNotNullOrBlank(p)) {
                    maven {
                        name = "OSSRH"
                        url = s ?: "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                        credentials {
                            username = u
                            password = p
                        }
                    }
                }
            }
        }
    }

    def confFile = file(getMybatisConfigFilePath(projectDir.absolutePath))
    if (confFile.exists() && confFile.isFile()) {

        def propertiesFile = file(getMybatisPropertiesFilePath(projectDir.absolutePath))
        def  propertiesPath = (propertiesFile.exists() && propertiesFile.isFile()) ?
                propertiesFile.absolutePath :
                getMybatisPropertiesFilePath(rootDir.absolutePath)
        def configPath = confFile.absolutePath

        configurations {
            mybatisGenerator
        }

        dependencies {
            mybatisGenerator "mysql:mysql-connector-java:$mysql_connector_version"
            mybatisGenerator "org.mybatis.generator:mybatis-generator-core:$mybatis_generator_version"
            mybatisGenerator "com.itfsw:mybatis-generator-plugin:$mybatis_gen_plug_version"
        }

        System.out.println()
        System.out.println("[$project.name] Find mybatis config:")
        System.out.println("properties: " + propertiesPath)
        System.out.println("configuration:" + configPath)
        System.out.println()

        task mybatisGenerate {
            doLast {

                ant.properties['targetProject'] = projectDir.path
                ant.properties['propertiesFile'] = propertiesPath

                ant.taskdef(
                        name: 'mbgenerator',
                        classname: 'org.mybatis.generator.ant.GeneratorAntTask',
                        classpath: configurations.mybatisGenerator.asPath
                )
                ant.mbgenerator(overwrite: true, configfile: configPath, verbose: true) {
                    propertyset {
                        propertyref(name: 'targetProject')
                        propertyref(name: 'propertiesFile')
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            def u = getPropertyOrCmdArgs(project, "PUB_USER", "u")
            def p =getPropertyOrCmdArgs(project, "PUB_PWD", "p")
            username = u
            password = p
        }
    }
}


